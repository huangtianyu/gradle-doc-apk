apply plugin: 'com.android.application'

ext.keyPswd = hasProperty("keyPswd")? getProperty("keyPswd"):"Gradledoc"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.githang.gradledoc"
        minSdkVersion 8
        targetSdkVersion 22
        versionCode 11
        versionName "0.4"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        myConfig {
            storeFile file("../gradle.jks")
            storePassword "Gradle"
            keyAlias "Gradle"
            keyPassword keyPswd
        }
    }

    buildTypes {
        debug {
            signingConfig  signingConfigs.myConfig
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig  signingConfigs.myConfig
        }
    }

    android.applicationVariants.all { variant ->
        if(variant.buildType.name == 'release') {
            variant.outputs.each { output ->
                def file = output.outputFile
                output.outputFile = new File(file.parent, "GradleUserGuide-"
                        + defaultConfig.versionName + "-" + variant.name + ".apk")
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'

    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'org.jsoup:jsoup:1.8.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
    // 状态栏颜色
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    // 友盟
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.githang:umeng-update:2.6.0.1'
    // json
    compile 'com.alibaba:fastjson:1.2.4'
    // android代码片段库
    compile 'com.githang:androidsnippet:0.6'
}